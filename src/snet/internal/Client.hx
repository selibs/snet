package snet.internal;

#if (nodejs || sys)
import haxe.io.Bytes;
import slog.Log;
import snet.Net;
import snet.internal.Socket;

#if !macro
@:build(ssignals.Signals.build())
#end
class Client {
	var socket:Socket;
	var logger:Logger = new Logger("CLIENT");

	public var isClosed(default, null):Bool = true;
	public var isSecure(default, null):Bool;
	public var certificate(default, null):Certificate;

	/**
		Absolute uri of a client
	**/
	public var uri(default, null):URI;

	/**
		Local side of a client.
	**/
	public var local(default, null):HostInfo;

	/**
		Remote side of a client.
	**/
	public var remote(default, null):HostInfo;

	@:signal function opened();

	@:signal function closed();

	@:signal function data(data:Bytes);

	public function new(uri:URI, connect:Bool = true, ?cert:Certificate):Void {
		if (uri != null) {
			this.uri = uri;
			remote = uri.host;
			isSecure = uri.isSecure;
			certificate = cert;

			if (connect)
				this.connect();
		}
	}

	function receive(data:Bytes) {
		this.data(data);
	}

	function connectClient() {}

	function closeClient() {}

	public function connect() {
		if (!isClosed)
			return;

		try {
			socket = new Socket();
			socket.connect(remote);
			isClosed = false;
			// socket.setBlocking(false);
			local = socket.host.info;
			logger.name = 'CLIENT $local - $remote';
			connectClient();
			logger.debug("Connected");
			opened();
			process();
		} catch (e) {
			logger.error('Failed to connect: $e');
			if (!isClosed) {
				socket.close();
				isClosed = true;
			}
		}
	}

	public function close() {
		if (isClosed)
			return;
		socket.close();
		isClosed = true;
	}

	public function send(data:Bytes) {
		try {
			if (isClosed)
				throw new NetError("Closed");
			socket.send(data);
		} catch (e)
			logger.error('Failed to send data: $e');
	}

	function process() {
		while (!isClosed) {
			if (!tick())
				break;
			Sys.sleep(0.01);
		}
		closeClient();
		if (!isClosed) {
			socket.close();
			isClosed = true;
		}
		logger.debug("Closed");
		closed();
	}

	function tick():Bool {
		try {
			var data = socket.read();
			if (data != null) {
				if (data.length > 0)
					receive(data);
				return true;
			} else
				logger.debug('Connection closed by peer');
		} catch (e)
			logger.error('Failed to tick: $e');
		return false;
	}

	function toString() {
		return logger.name;
	}
}
#end
